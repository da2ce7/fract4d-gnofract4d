dnl Process this file with autoconf to produce a configure script.

AC_INIT([gnofract4d],[2.0])
AC_CONFIG_SRCDIR(libfract4d/fract4d.h)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE
AC_PREFIX_DEFAULT(/usr)
dnl AC_MAINTAINER_MODE

AM_PROG_LIBTOOL
AC_PROG_INTLTOOL([0.21])

AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CXX
AM_PROG_CC_STDC
AC_HEADER_STDC

dnl libs required by libfract4d
AC_CHECK_LIB(pthread,pthread_join)
AC_CHECK_LIB(dl,dlopen)

dnl python
AM_PATH_PYTHON(2.0)

dmod="distutils.sysconfig"
AC_MSG_CHECKING(for Python $dmod module)
if $PYTHON -c "import $dmod" >/dev/null 2>&1; then
    AC_MSG_RESULT(found)
else
    AC_MSG_RESULT(not found)
    AC_MSG_ERROR([** Please install Python distutils modules **])
fi

AC_ARG_WITH(pythoninc,
	AC_HELP_STRING([--with-pythoninc],
		       [Select path for python headers]),
    	pythoninc=${withval},
	AC_MSG_CHECKING(for Python include dir)
	pythoninc=`$PYTHON -c "import $dmod; print $dmod.get_python_inc()"`
	AC_MSG_RESULT($pythoninc)
	AC_MSG_CHECKING(for Python library dir)
	pythonlibpath=`$PYTHON -c "import $dmod; print $dmod.get_config_var('LIBPL')"`
	AC_MSG_RESULT($pythonlibpath)
	AC_MSG_CHECKING(for Python library file)
	pythonlib=`$PYTHON -c "import $dmod; print $dmod.get_config_var('LIBRARY')"`
	AC_MSG_RESULT($pythonlib)

	AC_MSG_CHECKING(for Python library dependencies)
	pythondeps=`$PYTHON -c "import $dmod; print $dmod.get_config_var('LIBS')"`
	AC_MSG_RESULT($pythondeps)

	)
PY_CFLAGS="-I$pythoninc"
PY_OBJS="$pythonlibpath/$pythonlib"
PY_LIBS="$pythondeps"
AC_SUBST(PY_CFLAGS)
AC_SUBST(PY_OBJS)
AC_SUBST(PY_LIBS)

pkg_modules="libgnomeui-2.0"
PKG_CHECK_MODULES(GNOFRACT4D, [$pkg_modules])
AC_SUBST(GNOFRACT4D_CFLAGS)
AC_SUBST(GNOFRACT4D_LIBS)

GETTEXT_PACKAGE=gnofract4d
AC_DEFINE(GETTEXT_PACKAGE, "gnofract4d", "womble")
dnl AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [womble])
AC_SUBST(GETTEXT_PACKAGE)

dnl Add the languages which your application supports here.
ALL_LINGUAS="it"
AM_GLIB_GNU_GETTEXT

dnl look for gmp 
dnl AC_MSG_CHECKING(for GMP arbitrary-precision math library)
dnl AC_TRY_CPP(gmp.h,AC_DEFINE(HAVE_GMP) LIBS="$LIBS -lgmp")


dnl Subst PACKAGE_PIXMAPS_DIR.
PACKAGE_PIXMAPS_DIR="`gnome-config --datadir`/pixmaps/${PACKAGE}"
AC_SUBST(PACKAGE_PIXMAPS_DIR)

dnl Subst MAPS_DIR
PACKAGE_MAPS_DIR="`gnome-config --datadir`/maps/${PACKAGE}"
AC_SUBST(PACKAGE_MAPS_DIR)

dnl find marshaller generator path
GLIB_GENMARSHAL="`pkg-config --variable=glib_genmarshal glib-2.0`"
AC_SUBST(GLIB_GENMARSHAL)

AC_CONFIG_FILES([ 
Makefile
libfract4d/Makefile
fract4d/Makefile
src/Makefile
po/Makefile.in
doc/Makefile
doc/gnofract4d-manual/Makefile
doc/gnofract4d-manual/C/Makefile
pixmaps/Makefile
maps/Makefile
])
AC_OUTPUT

